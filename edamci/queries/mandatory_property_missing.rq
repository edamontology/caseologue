PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX edam:<http://edamontology.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT ?entity ?property ?label ?property_subs_edam WHERE {

    VALUES ?property {oboInOwl:hasDefinition
                       edam:created_in
                       #oboInOwl:inSubset
                       rdfs:label
                       rdfs:subClassOf }
    ?entity a owl:Class .

    FILTER NOT EXISTS {?entity owl:deprecated true .}
    OPTIONAL {?entity rdfs:label ?label .}
    FILTER ( ?entity != <http://edamontology.org/data_0006>)
    FILTER ( ?entity != <http://edamontology.org/topic_0003>)
    FILTER ( ?entity != <http://www.w3.org/2002/07/owl#DeprecatedClass>)
    FILTER ( ?entity != <http://edamontology.org/operation_0004>)
    FILTER ( ?entity != <http://edamontology.org/format_1915>)

    FILTER NOT EXISTS {?entity ?property ?value .
                       MINUS { ?value rdf:type owl:Restriction .} #to prevent concept with rdfs:subClassOf property being only restriction (e.g. has_topic)
                       }
    # UNION
    # {
    # VALUES ?property { oboInOwl:inSubset
    #                     }
    # FILTER NOT EXISTS {?entity ?property <http://purl.obolibrary.org/obo/edam#edam> .}
    # }
}
ORDER BY ?entity