# rule: the EDAM or OWL properties edam:is_deprecation_candidate, edam:is_refactor_candidate, 
# edam:isdebtag, edam:notRecommendedForAnnotation, owl:deprecated should only be defined if 
# they are booleans, and have the value 'true'
# implementation: this query returns all of the above mentioned properties with a value that is
# not of type xsd:boolean with a value set to 'true'
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX edam:<http://edamontology.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT ?entity ?property ?value WHERE {
  VALUES ?property {
    edam:is_deprecation_candidate
    edam:is_refactor_candidate
    edam:isdebtag
    edam:notRecommendedForAnnotation
    owl:deprecated  
}
  ?entity ?property ?value .
  FILTER (datatype(?value) != xsd:boolean || str(?value) != 'true' )  
  FILTER (!isBlank(?entity))
}
ORDER BY ?entity

